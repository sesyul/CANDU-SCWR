*Deck MultiCompoCelluleSCWR2DPert debug version
*--------------------
* Nom          : SCWR64PMulti.x2m
* Utilisation  : Creation de la base de donnees avec perturbations 
* Auteur       : U. Le Tennier 
* Date         : 23/09/2019
* Modifier     : /
*
*---------------------------
* DESCRIPTION DE LA PROCEDURE
* Utilisation des fichiers d edition et burnup perturbes pour enrichissement de la multicompo
* 64 crayons de combustible
*
*
* FICHIERS INTERNES
*
* VARIABLES INTERNES
* ITERATION     : Indice pour determiner l enrichissement a realiser 
* NbEtapesTot      : Nombre d iterations necessaires pour le calcul d evolution 
* Boucle        : Indice de boucle, combien de fois ont ete parcourus les pas d evolution
* Puissance     : Puissance de grappe               (kW/kg)
* DensCaloUp    : Densite du caloporteur ascendant  (g/cm3)
* DensCaloDw    : Densite du caloporteur descendant (g/cm3)
* DensMod       : Densite du moderateur             (g/cm3)
* ConcBore      : Quantite de bore a ajouter au mod (%mass) 
* TempFuel      : Temperature du combustible        (K)
* TempMod       : Temperature du moderateur         (K)
* TempCaloUp    : Temperature du caloporteur asc    (K)
* TempCaloDw    : Temperature du caloporteur dsc    (K)
*
* VARIABLES INTERNES 
* NumEtape      : Increment de boucle WHILE 
* CorrNum       : Correction de l increment pour sauvegarder dans le bon repertoire du fichier EDI 
* keff          : K-EFFECTIVE
* Sigma1        : Section efficace totale du premier groupe                             (b)
* Sigma2        : Section efficace totale du deuxieme groupe                            (b)
* Sigma3        : Section efficace totale du troisieme groupe                           (b)
* SigmaF1       : Section efficace fission du premier groupe                            (b)
* SigmaF2       : Section efficace fission du deuxieme groupe                           (b)
* SigmaF3       : Section efficace fission du troisieme groupe                          (b)
* BurnEtape     : Etape de burnup consideree, recuperee vie RepBurn
* Rep           : Repertoire ou sont stockees les informations dans chaque Edit
* NameSubDir    : Nom du sous-repertoire dans chaque Edit ou les informations d interpolation sont stockees
* RepBurn       : Repertoire du fichier ConcIso ou aller chercher le BURNUP-IRRAD pour le nombre de jours 
*    
*-------------------------------------------------------------------------
* Definition des modules, procedures et structures de donn√©es 
*-------------------------------------------------------------------------
MODULE GREP: COMPO: DELETE: END: UTL:                          ;
LINKED_LIST ConcPer34                                          ;
LINKED_LIST Edit34                                             ;
LINKED_LIST Db34                                               ;
SEQ_ASCII DbRef34 ; 
SEQ_ASCII DbRef34out        :: FILE './34out'                  ;
SEQ_ASCII ConcIsoRef34 ;
SEQ_ASCII EditPer34DCd ;
SEQ_ASCII EditPer34DCu ;
SEQ_ASCII EditPer34CB  ;
SEQ_ASCII EditPer34DM  ;
SEQ_ASCII EditPer34TCd ;
SEQ_ASCII EditPer34TCu ;
SEQ_ASCII EditPer34TF  ;
SEQ_ASCII EditPer34TM  ;
SEQ_ASCII EditPer34Pui ;
SEQ_ASCII ConcPer341 ; 
SEQ_ASCII ConcPer342 ; 
SEQ_ASCII ConcPer343 ; 
SEQ_ASCII ConcPer344 ; 

*--------------------------------------------------------------------------
* Variables locales et mise a jour du nombre d etapes
*--------------------------------------------------------------------------
INTEGER SCHEMATMP := 4 ; !3
INTEGER NbPert IndPert NumEtape NbEtapesTot := 0 0 0 0 ; 
INTEGER NbEtapes                                       ;
INTEGER NbEtao  NbEta2o NbEta3o NbEta4o NbEta5o        ;
INTEGER Saut                                           ; 
STRING  Rep Num RepCompo SousRep                       ;
REAL Puissance DensCaloUp DensCaloDw DensMod ConcBore
TempFuel TempMod TempCaloDw TempCaloUp NbJours         ;

INTEGER a := 1 ;


EVALUATE IndPert  := 1            ;
EVALUATE NbPert   := 7            ;
EVALUATE NumEtape := 1            ;
EVALUATE SousRep  := 'COMPO_DATA' ;

Db34 := DbRef34 ;

*--------------------------------------
* Determination des valeurs en fonction du nombre de cycles de la multicompo 
*--------------------------------------

**** ETAPES A SAUTER POUR METTRE DANS LES MULTICOMPO 

IF SCHEMATMP 3 = THEN 
  EVALUATE NbEtapes := 67  ;
ELSEIF SCHEMATMP 4 = THEN
  EVALUATE NbEtapes := 73  ;   
ENDIF ;

ConcPer34     := ConcIsoRef34  ;
EVALUATE RepCompo := 'Rep_34'  ;



WHILE IndPert NbPert < DO 

EVALUATE NumEtape  := 1              ;
EVALUATE Saut      := NbEtapes       ;


IF IndPert 1 = THEN
  Edit34    := EditPer34DCd ;
  EVALUATE NbEtapesTot := NbEtapes 4 * ;
ELSEIF IndPert 2 = THEN 
  Edit34    := EditPer34DCu ;
  EVALUATE NbEtapesTot := NbEtapes 4 * ;
ELSEIF IndPert 3 = THEN 
  Edit34    := EditPer34CB  ;
  EVALUATE NbEtapesTot := NbEtapes 4 * ;
ELSEIF IndPert 4 = THEN 
  Edit34    := EditPer34TCd ;
  EVALUATE NbEtapesTot := NbEtapes 3 * ;
ELSEIF IndPert 5 = THEN 
  Edit34    := EditPer34TCu ;
  EVALUATE NbEtapesTot := NbEtapes 4 * ;
ELSEIF IndPert 6 = THEN 
  Edit34    := EditPer34TF  ;
  EVALUATE NbEtapesTot := NbEtapes 4 * ;
ELSEIF IndPert 7 = THEN 
  Edit34     := EditPer34Pui ;

  EVALUATE NbEtapesTot := NbEtapes 4 * ;

  EVALUATE NbEtao    := NbEtapes 1 +                                ;
  EVALUATE NbEta2o   := NbEtapes 2 * 1 +                            ;
  EVALUATE NbEta3o   := NbEtapes 3 * 1 +                            ;
  EVALUATE NbEta4o   := NbEtapes 4 * 1 +                            ;
  EVALUATE NbEta5o   := NbEtapes 5 * 1 +                            ;

ENDIF                            ;

WHILE NumEtape NbEtapesTot < DO 

IF NumEtape 10 < THEN 
  EVALUATE Rep := 'REF-CASE000' NumEtape I_TO_S +              ;
  EVALUATE Num := '000' NumEtape I_TO_S +              ;
ELSEIF NumEtape 100 < THEN  
  EVALUATE Rep := 'REF-CASE00'  NumEtape I_TO_S +              ;
  EVALUATE Num := '00' NumEtape I_TO_S +              ;
ELSEIF NumEtape 1000 < THEN 
  EVALUATE Rep := 'REF-CASE0'   NumEtape I_TO_S +              ;
  EVALUATE Num := '0' NumEtape I_TO_S +               ;
ELSEIF NumEtape 10000 < THEN 
  EVALUATE Rep := 'REF-CASE'   NumEtape I_TO_S +              ;
  EVALUATE Num := '' NumEtape I_TO_S +               ;
ENDIF                                                         ;

*--------------------------------------
* Recuperation du bon fichier de burnup pour perturbation de puissance
*--------------------------------------

IF IndPert 7 = THEN 

  IF NumEtape 1 = THEN
    ConcPer34 := DELETE: ConcPer34 ;
    ConcPer34 := ConcPer341     ;
    EVALUATE a := 1 ;
  ELSEIF NumEtape NbEtao = THEN 
    ConcPer34 := DELETE: ConcPer34 ;
    ConcPer34 := ConcPer342     ;
    EVALUATE a := 2 ;
  ELSEIF NumEtape NbEta2o = THEN 
    ConcPer34 := DELETE: ConcPer34 ;
    ConcPer34 := ConcPer343     ;
    EVALUATE a := 3 ;
  ELSEIF NumEtape NbEta3o = THEN 
    ConcPer34 := DELETE: ConcPer34 ;
    ConcPer34 := ConcPer344  ;
    EVALUATE a := 4 ;
  ENDIF                          ;

ENDIF                            ;

GREP: Edit34 :: STEP UP <<Rep>>
                GETVAL <<SousRep>> 1 NVAL 10 
                >>Puissance<< >>DensCaloUp<< >>DensCaloDw<< 
                >>DensMod<<   >>ConcBore<<   >>TempFuel<< 
                >>TempMod<<   >>TempCaloUp<< >>TempCaloDw<<
                >>NbJours<<                              ;



IF IndPert 7 = THEN 
ELSE
  EVALUATE Puissance := 38.4 ;
  EVALUATE NbJours   := NbJours 57.61 * 38.4 / ;
ENDIF ;

ECHO "<.days.> " NbJours               ;

IF NumEtape Saut = THEN 
  EVALUATE Saut := Saut NbEtapes + ;
ELSE 

Edit34 := UTL: Edit34 :: 
*DEL 'LAST-EDIT' 
  CREA 'LAST-EDIT' 1 3 = 'REF-' 'CASE' <<Num>>  ;

Db34    := COMPO:  Db34 Edit34 ConcPer34  :: 
        EDIT 0
        STEP UP <<RepCompo>> 
         'Puissance'   <<Puissance>>
         'CaloUp Dens' <<DensCaloUp>>
         'CaloDw Dens' <<DensCaloDw>>
         'Mod Dens'    <<DensMod>> 
         'Bore ppm   '   <<ConcBore>>   
         'Fuel Temp'   <<TempFuel>>
         'Mod Temp'    <<TempMod>>
         'CaloUp Temp' <<TempCaloUp>>
         'CaloDw Temp' <<TempCaloDw>>
         SET <<NbJours>> DAY         ;

ENDIF ;

ECHO "<.ind.> " IndPert                                  ;
ECHO "<.rep.>" Rep IndPert                               ; 
ECHO "<.days.> " NbJours                                 ;

*--------------------------------------
* Suppresion des fichiers de burnup pour perturbation suivante
*--------------------------------------

EVALUATE NumEtape := NumEtape 1 +   ;


ENDWHILE ;

EVALUATE IndPert := IndPert 1 +    ;
Edit34           := DELETE: Edit34 ;

ENDWHILE ;

DbRef34out := Db34 ;


END: ;
QUIT "LIST" .